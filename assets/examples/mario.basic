Behaviour FollowHero
    Required Behaviour movement Of Type Movement

    Property hero

    Pre Update
        If x < hero.x Then movement.velocity.x = movement.velocity.x + 0.1
        If x > hero.x Then movement.velocity.x = movement.velocity.x - 0.1
        If y < hero.y Then movement.velocity.y = movement.velocity.y + 0.1
        If y > hero.y Then movement.velocity.y = movement.velocity.y - 0.1
    End
End



Class Hero Of Type Sprite
    Behaviour Of Type Joystick
    Behaviour Of Type Movement(speed=12)
    Behaviour Of Type Gravity
    Behaviour Of Type AirDrag
    Behaviour Of Type Jump(velocity=26)
    Behaviour Of Type TileCollision(layer=1)
    Behaviour Of Type Camera

    Constructor
        load(filename="./assets/sprites/mario-hero.png")
        show()
    End
End



Class GroundMonster Of Type Sprite
    Behaviour movement Of Type Movement(speed=5, velocity=New Vector(x=5, y=0))
    Behaviour Of Type Gravity
    Behaviour Of Type TileCollision(action=CollisionAction.bounceHorizontal)

    Property hero

    Constructor
        load(filename="./assets/sprites/mario-skeleton.png")
        show()
    End

    Update
        If movement.velocity.y == 0 Then
            If x > hero.x - 200 And x < hero.x + 200 Then movement.velocity.y = -10
        End
    End
End



Class FlyingMonster Of Type Sprite
    Behaviour Of Type FollowHero(hero=hero)
    Behaviour movement Of Type Movement(maxSpeed=5)
    Behaviour Of Type TileCollision

    Property hero

    Constructor
        load(filename="./assets/sprites/mario-monster.png")
        show()
    End
End



tilemap = Load Tilemap "./assets/tilemaps/super-mario.json"

hero = New Hero(x=200, y=150)
monster1 = New GroundMonster(x=100, y=150, hero=hero)
monster2 = New GroundMonster(x=500, y=150, hero=hero)
monster3 = New FlyingMonster(x=300, y=500, hero=hero)

While True Do
End
