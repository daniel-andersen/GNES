Class Hero Of Type Sprite
    Behaviour movement Of Type Movement(speed=13)
    Behaviour Of Type Joystick
    Behaviour Of Type Gravity
    Behaviour Of Type AirDrag
    Behaviour Of Type Jump
    Behaviour Of Type Camera
    Behaviour Of Type TileCollision(layer=1)

    Constructor
        load(filename="./assets/sprites/hero.png")
        show()
    End
End

Class GroundMonster Of Type Sprite
    Behaviour movement Of Type Movement
    Behaviour Of Type Gravity
    Behaviour Of Type TileCollision(action=CollisionAction.bounceHorizontal)

    Constructor
        load(filename="./assets/sprites/skeleton.png")
        show()
        movement.speed = 5
        movement.velocity = New Vector(x=movement.speed, y=movement.speed)
    End

    Update
        If x < 0 Then movement.velocity.x = movement.speed
        If x > World.width Then movement.velocity.x = -movement.speed

        If movement.velocity.y == 0 Then
            If x > hero.x - 200 And x < hero.x + 200 Then movement.velocity.y = -10
        End
    End
End

Class FlyingMonster Of Type Sprite
    Behaviour movement Of Type Movement(maxSpeed=5)
    Behaviour Of Type TileCollision

    Constructor
        load(filename="./assets/sprites/monster.png")
        show()
        movement.speed = 4
    End

    Pre Update
        If x < hero.x Then movement.velocity.x = movement.velocity.x + 0.1
        If x > hero.x Then movement.velocity.x = movement.velocity.x - 0.1
        If y < hero.y Then movement.velocity.y = movement.velocity.y + 0.1
        If y > hero.y Then movement.velocity.y = movement.velocity.y - 0.1
    End
End

tilemap = Load Tilemap "./assets/tilemaps/super-mario.json"

hero = New Hero(x=200, y=150)
monster1 = New GroundMonster(x=100, y=150, hero=hero)
monster2 = New GroundMonster(x=500, y=150, hero=hero)
monster3 = New FlyingMonster(x=300, y=500, hero=hero)

While True Do
End
