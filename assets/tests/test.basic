Class Monster Of Type Sprite
    Property direction = New Vector2D(x=1, y=1)
    Property speed = 4.0

    Constructor
        load(filename="./assets/sprites/monster.png")
        show()
        x = 300
        y = 300
    End

    Update
        x = x + Screen.frameSpeed * direction.x * speed
        If x < 0 Then direction.x = 1
        If x > Screen.width Then direction.x = -1

        y = y + Screen.frameSpeed * direction.y * speed
        If y < 0 Then direction.y = 1
        If y > Screen.height Then direction.y = -1
    End
End

Class Hero Of Type Sprite
    Property speed = 8.0
    Behaviour joystick Of Type Joystick
    Behaviour gravity Of Type Gravity

    Constructor
        load(filename="./assets/sprites/hero.png")
        show()
        x = 200
        y = Screen.height - 100
    End

    Update
        If y > Screen.height - 100 Then
            y = Screen.height - 100
            gravity.velocity = 0
        End
        If Joystick.up And y == Screen.height - 100 Then
            gravity.velocity = -20.0
        End

        If Joystick.left And x > 0 Then
            x = x - Screen.frameSpeed * speed
        End
        If Joystick.right And x < Screen.width Then
            x = x + Screen.frameSpeed * speed
        End
    End
End

monster1 = New Monster(speed=4.0)
monster2 = New Monster(speed=6.0)
hero = New Hero()

Function followHero()
    speed = 3.0
    monster = Load Sprite "./assets/sprites/monster.png"
    monster.x = Screen.width / 2
    monster.y = hero.y
    Show Sprite monster
    While True Do
        If monster.x < hero.x Then monster.x = monster.x + Screen.frameSpeed * speed
        If monster.x > hero.x Then monster.x = monster.x - Screen.frameSpeed * speed
        Wait For Update
    End
End

Run followHero()
