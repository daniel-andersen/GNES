Class Hero Of Type Sprite
    Behaviour movement Of Type Movement(speed=13)
    Behaviour Of Type Joystick
    Behaviour Of Type Gravity
    Behaviour Of Type AirDrag
    Behaviour Of Type Jump
    Behaviour Of Type Camera
    Behaviour Of Type Ground(layer=1)

    Constructor
        load(filename="./assets/sprites/hero.png")
        show()
    End
End

Class Monster Of Type Sprite
    Behaviour movement Of Type Movement

    Property speed = 4.0

    Constructor
        load(filename="./assets/sprites/monster.png")
        show()
        movement.velocity = New Vector(x=speed, y=speed)
    End

    Update
        If x < 0 Then movement.velocity.x = speed
        If x > Screen.width Then movement.velocity.x = -speed

        If y < 0 Then movement.velocity.y = speed
        If y > Screen.height Then movement.velocity.y = -speed
    End
End

tilemap = Load Tilemap "./assets/tilemaps/super-mario.json"

monster1 = New Monster(x=100, y=200, speed=4.0)
monster2 = New Monster(x=700, y=500, speed=6.0)
hero = New Hero(x=200, y=150, anchor=New Vector(x=0.5, y=1.0))

Function followHero()
    speed = 3.0
    monster = Load Sprite "./assets/sprites/monster.png"
    monster.x = Screen.width / 2
    monster.y = Screen.height - 100
    Show Sprite monster

    While True Do
        If monster.x < hero.x Then monster.x = monster.x + Screen.frameSpeed * speed
        If monster.x > hero.x Then monster.x = monster.x - Screen.frameSpeed * speed
        If monster.y < hero.y Then monster.y = monster.y + Screen.frameSpeed * speed
        If monster.y > hero.y Then monster.y = monster.y - Screen.frameSpeed * speed
        Wait For Update
    End
End

Run followHero()
